apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.source
}

artifacts {
    archives androidSourcesJar
}

publishing {
    publications {
        release(MavenPublication) {
            groupId 'com.atinternet'
            artifactId 'Tracker'
            version '2.21.2'

            artifact(project.buildDir.absolutePath + "/outputs/aar/${project.getName()}-release.aar")
            artifact androidSourcesJar

            pom {
                name = 'com.atinternet'
                description = 'ATInternet Mobile Analytics Solutions for Android.'
                url = 'https://github.com/at-internet/atinternet-android-sdk'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'at-internet'
                        name = 'AT Internet'
                        email = 'teamdatacollection@atinternet.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:at-internet/atinternet-android-sdk.git'
                    developerConnection = 'scm:git@github.com:at-internet/atinternet-android-sdk.git'
                    url = 'scm:git@github.com:at-internet/atinternet-android-sdk.git'
                }
            }
        }
    }
    repositories {
        maven {
            name = "sonatype"
            url = version.endsWith('SNAPSHOT') ? 'https://oss.sonatype.org/content/repositories/snapshots/' : 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'

            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications
}

nexusStaging {
    packageGroup = 'com.atinternet'
}