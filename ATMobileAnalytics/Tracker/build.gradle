apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        abortOnError false
    }
}

def siteUrl = 'https://github.com/at-internet/atinternet-android-sdk'
def gitUrl = 'https://github.com/at-internet/atinternet-android-sdk.git'
group = "com.atinternet"
version = "2.3.5"

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'ATInternet Mobile Analytics Solutions for Android'
                url siteUrl
                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "tracker"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = true
    }
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    provided 'com.google.android.gms:play-services-ads:10.2.4'
    testCompile 'org.robolectric:robolectric:3.3.2'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    delete 'technicalDoc/' + version
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = new File(projectDir, "technicalDoc/" + version)

    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.header = "ATInternet Android SDK API " + version
    options.stylesheetFile = new File(projectDir, "docs-res/style.css");
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

task createAlphaDelivery {
    doLast {
        def destination = 'delivery/' + version + 'a/'
        copy {
            from 'build/outputs/aar/'
            into destination
            include('Tracker-release.aar')
            rename('Tracker-release.aar', 'Tracker.aar')
        }
        copy {
            from 'build/intermediates/bundles/default/'
            into destination
            include('classes.jar')
            rename('classes.jar', 'Tracker.jar')
        }
        copy {
            from '.'
            include '*.manifest.json'
            into destination
        }
    }
}

task createProdDelivery {
    doLast {
        def destination = 'delivery/' + version + '/'
        copy {
            from 'build/outputs/aar/'
            into destination
            include('Tracker-release.aar')
            rename('Tracker-release.aar', 'Tracker.aar')
        }
        copy {
            from 'build/intermediates/bundles/default/'
            into destination
            include('classes.jar')
            rename('classes.jar', 'Tracker.jar')
        }
        copy {
            from '.'
            include '*.manifest.json'
            into destination
        }
        task(createZipFromJar, type: Zip) {
            from destination
            include 'Tracker.jar'
            archiveName 'ATInternet_Android_SDK_' + version + '.zip'
            destinationDir projectDir
        }.execute()
    }
}

task clearDelivery(type: Delete) {
    delete 'delivery/'
    delete 'technicalDoc/'
    delete 'ATInternet_Android_SDK_' + version + '.zip'

}

createAlphaDelivery.dependsOn(clearDelivery, build)
createProdDelivery.dependsOn(clearDelivery, build, javadoc)
