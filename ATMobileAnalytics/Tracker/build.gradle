apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'jacoco'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.1'
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

def siteUrl = 'https://github.com/at-internet/atinternet-android-sdk'
def gitUrl = 'https://github.com/at-internet/atinternet-android-sdk.git'
group = "com.atinternet"
version = "2.10.0"

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'ATInternet Mobile Analytics Solutions for Android'
                url siteUrl
                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "tracker"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    compileOnly 'com.google.android.gms:play-services-ads:15.0.1'
    testImplementation 'org.robolectric:robolectric:3.3.2'
    testImplementation 'junit:junit:4.12'
}

clean {
    delete project.rootDir.absolutePath + '/delivery/'
    delete project.rootDir.absolutePath + '/documentation/'
    delete project.rootDir.absolutePath + '/build.properties'
    delete project.rootDir.absolutePath + '/ATInternet_Android_SDK_' + version + '.zip'
}

task sourcesJar(type: Jar) {
    from project.rootDir.absolutePath + '/Tracker/src/main/java'
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

task createDelivery {
    def destination = project.rootDir.absolutePath + '/delivery/' + version + '/'
    doLast {
        copy {
            from 'build/outputs/aar/'
            into destination
            include('Tracker-release.aar')
            rename('Tracker-release.aar', 'Tracker.aar')
        }
        copy {
            from 'build/intermediates/packaged-classes/release/'
            into destination
            include('classes.jar')
            rename('classes.jar', 'Tracker.jar')
        }
        copy {
            from '.'
            include '*.manifest.json'
            into destination
        }
        task(createZipForAnalyser, type: Zip) {
            from destination
            include 'Tracker.jar'
            archiveName 'ATInternet_Android_SDK_' + version + '.zip'
            destinationDir project.rootDir
        }.execute()
    }
}

task createBuildFile {
    def file = new File(project.rootDir, "build.properties")
    file.createNewFile()
    file.text = "version=" + version + "\n"
}

jacoco {
    toolVersion = "0.7.1.201405082137"
}

def coverageSourceDirs = [
        project.rootDir.absolutePath + '/Tracker/src/main/java/com/atinternet/Tracker'
]

task jacocoTestReport(type: JacocoReport, dependsOn: "testReleaseUnitTest") {
    group = "Reporting"

    description = "Generate Jacoco coverage reports"

    classDirectories = fileTree(
            dir: project.rootDir.absolutePath + '/Tracker/build/intermediates/classes/release/com/atinternet/tracker',
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/*$ViewInjector*.*',
                       '**/BuildConfig.*',
                       '**/Manifest*.*']
    )

    additionalSourceDirs = files(coverageSourceDirs)
    sourceDirectories = files(coverageSourceDirs)
    executionData = files(project.rootDir.absolutePath + '/Tracker/build/jacoco/testReleaseUnitTest.exec')

    reports {
        xml.enabled = true
        html.enabled = true
    }
}